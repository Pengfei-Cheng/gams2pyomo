?start: (statement | WS | ao_macro)*

statement: NOT_AO_MACRO NL

ao_macro: _AO_MACRO  command NL

command: "list"  item_exp 							-> cmd_list
		| "map" project ( item_exp | selector )		-> cmd_map
		| "param" item selector						-> cmd_param
		| "tariff" tariff_type						-> cmd_tariff
		| "json2data" data_select template~0..5		-> cmd_jsondata

item_exp: item [filter]

tariff_type: "weekend_schedule" 	-> tariff_sched_weekend
			| "weekday_schedule"	-> tariff_sched_weekday
			| "product_rate"		-> tariff_rate
			| "product_adj"			-> tariff_rate_adj

data_select: WORD_SELECTOR 				-> data_by_key
			| "group=" WORD_SELECTOR	-> data_by_group

?item: asset
	| project
	| data

asset: _ASSET
project: _PROJECT
data: _DATA

_PROJECT: 	"project"
_ASSET: 	"asset"
_DATA: 		"data"


selector: "'" WORD_SELECTOR "'"
template: "\"" WORD_TEMPLATE+ "\""
filter: "\"" get_param op  value "\""
op: "==" -> op_eq
	| "startswith" -> op_sw
?get_param: WORD
?value: WORD_AO


_WS 			: WS
_NL 			: NEWLINE
NL  			: NEWLINE

NOT_AO_MACRO	: /(?!\*\*ao).+/
_AO_MACRO		: "**ao"
ANY				: /.+/
WORD			: /[\w-]+/
WORD_AO 		: /[a-zA-Z][\w:]*/
WORD_SELECTOR	: /[a-zA-Z][\w:\.-]*/
WORD_TEMPLATE	: /[\w:\.\{\}\t\(\)\[\]]+/

%ignore WS

%import common.WS
%import common.NEWLINE

